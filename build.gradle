plugins {
	id 'java'
	id 'eclipse'
	id 'net.saliman.cobertura' version '2.2.6'
	id "org.sonarqube" version "1.0"
	id "com.jfrog.bintray" version "1.4"
	id "maven"
	id "maven-publish"
	id "com.github.johnrengelman.shadow" version "1.2.4"

	id "synapticloop.copyrightr" version "1.1.2"
	id "synapticloop.documentr" version "2.9.0"
}

version = '2.0.0'

group = 'synapticloop'
archivesBaseName = 'routemaster'
description = """lightweight web server with RESTful routing"""

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
}

dependencies {
	runtime 'synapticloop:templar:1.4.2'
	runtime 'org.nanohttpd:nanohttpd:2.3.1'

	compile 'synapticloop:templar:1.4.2'
	compile 'org.nanohttpd:nanohttpd:2.3.1'

	testCompile 'junit:junit:4.11'
	testCompile 'org.mockito:mockito-all:1.10.19'
	testCompile 'synapticloop:templar:1.4.2'
}

jar {
	manifest {
		attributes("Main-Class": "synapticloop.nanohttpd.RouteMasterServer",
				"Implementation-Version": version)

	}
}

shadowJar {
	classifier = ""
	destinationDir = libsDir
	baseName = 'routemaster-templar'
	append('/src/main/html/templar/*')
}



cobertura {
	coverageFormats = [ 'html', 'xml']
}

test {
	include '**/*Test.class' 
	maxParallelForks = 5
	forkEvery = 50 
}


ant.version = version
ant.buildDir = buildDir

task zip << {
	ant.zip(destfile: '${buildDir}/libs/routemaster-example-${version}.zip') {
		fileset(dir: 'src/main/html') {
			include(name: '**/*.*')
		}

		fileset(dir: '${buildDir}/libs/') {
			include(name: 'routemaster-templar-${version}.jar')
		}
	}
}

zip.mustRunAfter shadowJar

def javaApiUrl = 'http://docs.oracle.com/javase/1.7.0/docs/api/'
def groovyApiUrl = 'http://groovy.codehaus.org/gapi/'

tasks.withType(Javadoc) {
	options.links(javaApiUrl, groovyApiUrl)
	exclude '**/fi/**'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

publishing {
	publications {
		Synapticloop(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar

			groupId 'synapticloop'
			artifactId 'routemaster'

			pom.withXml {
				configurations.compile.resolvedConfiguration.firstLevelModuleDependencies.each { dep ->
					asNode().dependencies[0].dependency.find {
						it.artifactId[0].text() == dep.moduleName &&
								it.groupId[0].text() == dep.moduleGroup
					}.scope[0].value = 'compile'
				}
			}
		}
	}
}

bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_PASSWORD')

	publications = [ 'Synapticloop' ]

	publish = true

	pkg {
		repo = 'maven'
		name = 'routemaster'
	}

}

task(dist).dependsOn( [ 'clean', 'shadowJar', 'cobertura', 'jar', 'javadoc' ] )

task(testMain, dependsOn: jar, type: JavaExec ) {
	main = 'synapticloop.h2zero.Main'
	classpath = sourceSets.main.runtimeClasspath
}